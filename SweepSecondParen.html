<!DOCTYPE html>
<!--
SweepSecond (C) 2013 KIM Taegyoon

# License

Copyright 2013 KIM Taegyoon

Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
-->
<html>
<head>
<meta charset="utf-8">
<title>SweepSecond</title>
</head>
<body style="background-color:black">
<div id="label1" style="color:white; text-align:center"></div>
<center><canvas id="canvas"></canvas></center>

<script src="paren.js"></script>
<script>if(typeof(paren)==='undefined')document.write('<script src="http://jsutil.sourceforge.net/paren.js"><\/script>')</script>

<script type="text/paren">
(defn Point (X Y) (set this.X X) (set this.Y Y))
(defn toRadians (ratio) (+ (* (- 0 ratio) Math.PI 2) Math.PI))
(defn drawHand (ctx center radius radians sideWeight)
  (def pTo (new Point
    (+ center.X (* radius (Math.sin radians)))
    (+ center.Y (* radius (Math.cos radians)))))
  (def pFrom (new Point
                  (+ center.X (* (/ radius 8) (Math.sin (- radians Math.PI))))
                  (+ center.Y (* (/ radius 8) (Math.cos (- radians Math.PI))))))
  (def pSide1 (new Point
                   (+ center.X (* (/ radius 20) sideWeight (Math.sin (- radians (/ Math.PI 2)))))
                   (+ center.Y (* (/ radius 20) sideWeight (Math.cos (- radians (/ Math.PI 2)))))))
  (def pSide2 (new Point
                   (+ center.X (* (/ radius 20) sideWeight (Math.sin (+ radians (/ Math.PI 2)))))
                   (+ center.Y (* (/ radius 20) sideWeight (Math.cos (+ radians (/ Math.PI 2)))))))
  (ctx.beginPath)
  (ctx.moveTo pFrom.X pFrom.Y)
  (ctx.lineTo pSide1.X pSide1.Y)
  (ctx.lineTo pTo.X pTo.Y)
  (ctx.lineTo pSide2.X pSide2.Y)
  (ctx.fill))
  
(defn drawTick (ctx center radius len radians)
  (def pTo (new Point
    (+ center.X (* radius (Math.sin radians)))
    (+ center.Y (* radius (Math.cos radians)))))
  (def pFrom (new Point
    (+ center.X (* radius (- 1 len) (Math.sin radians)))
    (+ center.Y (* radius (- 1 len) (Math.cos radians)))))
  (ctx.beginPath)
  (ctx.moveTo pFrom.X pFrom.Y)
  (ctx.lineTo pTo.X pTo.Y)
  (ctx.stroke))
  
(defn refresh ()
  (def canvas (document.getElementById "canvas"))
  (def ctx (canvas.getContext "2d"))  
  
  (def now (new Date))
  (def hour (now.getHours))
  (def minute (now.getMinutes))
  (def second (+ (now.getSeconds) (/ (now.getMilliseconds) 1000)))

  (set (.get (document.getElementById "label1") innerHTML) (now.toString))

  (def size (- (Math.min window.innerWidth window.innerHeight) 50))
  
  (set canvas.width size)
  (set canvas.height size)
  (ctx.translate 5 5)
  (ctx.scale 0.98 0.98)
  
  (def r (/ size 2))
  (def center (new Point r r))

  (def radSec (toRadians (/ second 60)))
  (def radMin (toRadians (/ (+ minute (/ second 60)) 60)))
  (def radHour (toRadians (/ (+ (% hour 12) (/ minute 60)) 12)))
  
  (set ctx.fillStyle "Black")
  (ctx.fillRect 0 0 size size)

  ; minute ticks
  (set ctx.strokeStyle "White")
  (set ctx.lineWidth (/ r 120))
  (for/s (def rad 0.0) (< rad (* Math.PI 2.0)) (+= rad (/ (* Math.PI 2.0) 60.0))
      (drawTick ctx center (* r 0.95) 0.06 rad))

  ; hour ticks
  (set ctx.lineWidth (/ r 60))
  (def rad 0)
  (for/s (def h 0) (< h 12) (++ h)
    (set ctx.lineWidth (if (== 0 (% h 3)) (/ r 30) (/ r 60)))
    (drawTick ctx  center (* r 0.95) 0.2 rad)
    (+= rad (/ (* Math.PI 2.0) 12.0)))

  ; rim  
  (set ctx.lineWidth (/ r 60))
  (ctx.beginPath)
  (ctx.arc r r r 0 (* Math.PI 2) false)
  (ctx.stroke)

  ; hands
  (set ctx.fillStyle "DeepSkyBlue")
  (drawHand ctx center (* r 0.5) radHour (/ 1.2 0.5))
  (set ctx.fillStyle "LawnGreen")
  (drawHand ctx center (* r 0.8) radMin (/ 1.0 0.8))
  (set ctx.fillStyle "Crimson")
  (set radSec (+ radSec (* 0.1 (Math.sin (* 0.001 (now.getMilliseconds))))))
  (set radSec (+ radSec (* 0.02
		           (/ (- 1000 (now.getMilliseconds)) 1000)
		           (Math.sin (* 0.1 (now.getMilliseconds))))))
  (drawHand ctx  center (* r 0.9) radSec (/ 0.5 0.9))

  ; center
  (def r2 (/ r 20.0))
  (set ctx.fillStyle "White")
  (ctx.beginPath)
  (ctx.arc r r r2 0 (* Math.PI 2) false)
  (ctx.fill))

(defn init ()
  (refresh)
  (setInterval refresh 33))
(init)
</script>
</body>
</html>
